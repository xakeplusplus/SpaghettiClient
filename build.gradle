buildscript {
    repositories {
        maven {
            name = "SpongePowered"
            url  = "https://repo.spongepowered.org/repository/maven-public/"
        }

        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}


apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'org.spongepowered.mixin'

version = "1.0"
group = "me.xakeplusplus.spg"
archivesBaseName = "SpaghettiClient"

minecraft {
    version  = "1.12.2-14.23.0.2531"
    runDir   = "run"
    coreMod  = "me.xakeplusplus.spg.mixin.MixinLoader"
    mappings = "snapshot_20180814"
    makeObfSourceJar = true
}

repositories {
    maven {
        name = "spongepowered-repo"
        url  = "https://repo.spongepowered.org/maven/"
    }

    maven {
        name = "swt-repo"
        url  = "https://maven-eclipse.github.io/maven"
    }

    maven {
        name = "jitpack.io"
        url  = "https://jitpack.io"
    }

    mavenCentral()
}

dependencies {

    compile group: 'net.jodah', name: 'typetools', version: '0.5.0'
    compile group: "com.googlecode.json-simple", name: "json-simple", version: "1.1.1"
    compile group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.1'

    compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: "commons-io"
        exclude module: "guava"
        exclude module: "gson"
    }

}

shadowJar {
    dependencies {
        include(dependency('net.jodah:typetools'))
        include(dependency("org.javassist:javassist"))
        include(dependency("org.spongepowered:mixin"))
        include(dependency('com.googlecode.json-simple:json-simple:1.1.1'))
        include(dependency('club.minnced:java-discord-rpc:2.0.1'))
    }
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.spg.refmap.json"
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath   = sourceSets.main.compileClasspath
    }
}

processResources {
    inputs.property "version",   project.version
    inputs.property "mcversion", project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version" : project.version, "mcversion" : project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename "(.+_at.cfg)", 'META-INF/$1'
}

jar {
    manifest {
        attributes (
                'MixinConfigs': 'mixins.spg.json',
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': 'me.xakeplusplus.spg.SpaghettiClient',
                'ForceLoadAsMod': 'true',
                'FMLAT': '***_at.cfg'
        )
    }
}

build.dependsOn(shadowJar)